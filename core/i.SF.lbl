	&"{clear}{blue}{pound}vj{lt. blue}{rvrs off} {rvrs on} Sysop File Maintenance{pound}{back arrow}39{rvrs off}{f6}{blue}{pound}vj{green}":gosub{:3176}:goto{:3012}
{:3002}
	dr=3:a$="e.sysop,l,"+chr$(250):goto[4]
{:3004}
	a=abs(int(val(an$))):return
{:3006}
	dr=3:a$="e.sysop":goto[4]
{:3008}
	&"{f6:2}Error Opening Sysop File"
{:3010}
	close2:&"{f6:2}{white}{rvrs on}Any Key:{rvrs off}{green} {pound}g1"
{:3012}
	p$="":&"{f6:2}Options:{f6}1. Create Sysop File{f6}2. Edit Sysop Record{f6}3. Delete Sysop"
	&"{f6}4. Change File to {f5}n{f5}{f6}5. Add Sysop{f6}6. Quit{f6:2}Choice? {pound}g1{pound}v7{f6}"
	a=val(an$):ifa<1ora>6then[300]
	onagoto{:3020},{:3030},{:3092},{:3168},{:3104},[300]:goto{:3012}
{:3020}
	&"{clear}Create File{f6:2}Continue? ":gosub[96]:ifa=.then{:3012}
	&"{f6:2}Creating...":gosub{:3002}:ife%then:&"{f6:2}Error Creating File.":goto{:3010}
	&"{pound}h6ed...{f6:2}Filling: ":forx=1tour
	&mid$("{white}{cyan}{purple}{green}{yellow}{lt. red}{lt. green}{lt. blue}{gray3}",int(rnd(1)*9)+1,1)+mid$("{^}*+{pound}=[]$#",int(rnd(1)*9)+1,1)
	gosub[1]:print#2,"{^}":next:&"{f6:2}{lt. green}Finished!!":goto{:3010}
{:3030}
	&"{clear}Edit Sysop Record:"
{:3032}
	&"{f6:2}Enter The HANDLE Or ID# Of The Sysop:{f6:2}"
	nn$="":poke53252,20:gosub[6]:gosub{:3004}:ifan$=""then{:3012}
	ifa=.thennn$=an$:goto{:3048}
	ifa>urthen:&"{f6:2}Out Of Range. ":goto{:3032}
	i=a:gosub[35]:x=a
	gosub[1]:&,2,2:on-(a$="{^}")goto{:3052}:nn$=a$
	b$=nn$:&"{f6}Is {f5}{pound}$b{f5} Correct?: ":gosub[96]:ifa=.then{:3032}
	close2:poke53252,30:goto{:3054}
{:3048}
' changed 45 -> 445 (sub.handle instead of e.say stuff)
	im=10:gosub 445:ifi=.then lp=1:&"{f6}{f5}{pound}v7{f5} Not Found in User File.":goto{:3032}
	nn$=an$:x=i:goto{:3054}
{:3052}
	&"{f6}":&nn$:&" Doesn't Exist.{f6}":goto{:3032}
{:3054}
	gosub{:3006}:on-(e%<>.)goto{:3008}:gosub[1]:&,2,2
	ifa$="n"ora$="{^}"then:&"{f6:2}No Data.  Select Add Sysop.":goto{:3010}
	gosub[1]:fori=.to13:&,2,2:ifasc(a$+nl$)=32thena$=mid$(a$,2)
	tt$(i)=a$:next
{:3062}
	&"{f6:2}Edit...":fori=1to13
	&"{f6}{pound}!i{pound}{back arrow}04":&ed$(i):&"{pound}{back arrow}20:":&tt$(i):next:&"{f6:2}Which Field? {pound}i1"
	ontr%+1goto[300]:ifan$=""then{:3012}
	i=val(an$):ifi<1ori>13then:&"{f6:2}Out of Range!{pound}w2":goto{:3062}
	&"{f6:2}Change ":&ed$(i):&" ?":gosub[96]:ifa=.then{:3062}
	ifi<=3ori=8ori=13then:&"{f6:2}New Data For :":&ed$(i):&"{f6:2}[> {pound}i1":goto{:3076}
	&"{f6:2}New Data For :":&ed$(i):&"{f6:2}[> {pound}i0"
{:3076}
	ontr%+1goto[300]:on-(an$="")goto{:3090}:tt$=an$:&"{f6:2}Is ":&tt$:&" Correct? "
	gosub[96]:ifa=.then{:3090}
	tt$(i)=tt$:&"{f6:2}Other Changes? ":gosub[96]:onagoto{:3062}:tt$(.)=d1$
	iftt$(12)=""thentt$(12)="None"
	iftt$(13)=""thentt$(13)="N/A"
	w$="":fori=.to13:w$=w$+tt$(i)+r$:next:gosub[1]:print#2,w$
	&"{f6:2}Changes Complete...":goto{:3010}
{:3090}
	&"{f6:2}Aborted! Try Again? ":gosub[96]:onagoto{:3062}:goto{:3010}
{:3092}
	&"{clear}Delete A Sysop.{f6:2}ID Number To Delete? {pound}i1":x=val(an$)
	ontr%+1goto[300]:on-(an$="")goto{:3012}:ifx<1orx>urthen{:3092}
	gosub{:3006}:gosub[1]:&,2,2:ifa$="n"ora$="{^}"then:&"{f6:2}Deleted!{pound}w2":goto{:3092}
	&,2,2:&"{f6:2}Delete {pound}$a? ":gosub[96]:ifa=.then{:3092}
	gosub[1]:print#2,"n":&"{f6:2}Sysop Deleted.  Another? ":gosub[94]
	onagoto{:3092}:goto{:3010}
{:3104}
	&"{clear}Add A Sysop.{f6:2}The Following Information Is Required."
{:3106}
	&"{f6:2}Enter The HANDLE Or ID# Of The Sysop:{f6:2}"
	nn$="":poke53252,20:gosub[6]:gosub{:3004}:ifan$=""then{:3012}
	ifa>urthen:&"{f6:2}Out Of Range. ":goto{:3106}
	on-(a=.)goto{:3120}:i=a:gosub[35]:x=a
	gosub[1]:&,2,2:on-(a$="{^}")goto{:3126}:b$=a$
	&"{f6}Is {f5}{pound}$b{f5} Correct?: ":gosub[96]:ifa=.then{:3106}
	fl=1:nn$=b$:close2:goto{:3126}
{:3120}
	gosub[505]:ifi=.then:&"{f6}{f5}{pound}v7{f5} Not Found{f6}In User File.{f6}":goto{:3106}
	nn$=an$:x=i:fl=1:goto{:3126}
	&"{f6}":&nn$:&" Doesn't Exist.{f6}":goto{:3106}
{:3126}
	tt$(.)=d1$:ifflthentt$(1)=nn$:fori=2to3:goto{:3130}
	fori=1to3
{:3130}
	&"{white}":&ed$(i):&"{pound}{back arrow}13: {pound}i1":on-(an$="")goto{:3012}:tt$(i)=an$:next
	poke53252,30:fori=4to13
	ifi<>8ori<>13then:&ed$(i):&"{pound}{back arrow}13: {pound}i0":goto{:3138}
	ifi=8ori=13then:&ed$(i):&"{pound}{back arrow}13: {pound}i1"
{:3138}
	on-(an$="")goto{:3140}:tt$(i)=an$
{:3140}
	next
{:3142}
	&"{f6:2}Your Input: ":fori=1to13
	&"{f6}{pound}!i{pound}{back arrow}05:":&tt$(i):next
	&"{f6:2}Correct? ":gosub[96]:onagoto{:3160}
	&"{f6:2}Which Number Is Not Correct? {pound}i1":on-(an$="")goto{:3160}:i=val(an$)
	&"{f6}":&ed$(i):&"{pound}{back arrow}13:":&tt$(i)
	ifi<=3ori=8ori=13then:&"New :":&ed$:&"{f6}]> {pound}i1":tt$=an$:ontr%+1goto[1811]:goto{:3156}
	&"{f6}New :"ed$(i)"{f6}]> {pound}i0":ontr%+1goto[300]:tt$=an$
{:3156}
	on-(an$="")goto{:3142}:&"{f6:2}Is {pound}v7 Correct? ":gosub[96]:ifa=.then{:3142}
	tt$(i)=tt$:goto{:3142}
{:3160}
	&"{f6:2}Writing Data...":tt$(.)=d1$:iftt$(12)=""thentt$(12)="None"
	iftt$(13)=""thentt$(13)="N/A"
	w$="":fori=.to13:w$=w$+tt$(i)+r$:next
	gosub{:3006}:gosub[1]:print#2,w$:&"{f6:2}Added!":goto{:3010}
{:3168}
	&"{f6}Change file to {f5}n{f5}"
	&"{f6:2}ID Number: {pound}i1":ifan$=""then{:3010}
	ontr%+1goto[300]:x=val(an$):ifx<1orx>urthen:&"{f6:2}Out of range.":goto{:3010}
	gosub{:3006}:gosub[1]:print#2,"n":&"{f6}Done!":goto{:3010}
{:3176}
	ed$(1)="Handle":ed$(2)="First Name":ed$(3)="Last Name"
	ed$(4)="Address":ed$(5)="City":ed$(6)="State":ed$(7)="Zipcode"
	ed$(8)="Serial Number":ed$(9)="Board Name":ed$(10)="Voice Number"
	ed$(11)="Telnet Addr.":ed$(12)="Network":ed$(13)="Node ID":return
	rem copr. 1994 new image 10/10/94
	rem fixes 181220 x-tec, 2021-07-06 pinacolada
	rem "i.SF (c)nissa 181231 lh"
