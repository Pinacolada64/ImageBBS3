{uses:3_0-preface.lbl}
{alpha:alt}
{:4000}
	gosub {:4480}:onlmgoto {:4100},{:4200},{:4010}:mm=.:f1=3:goto {:3000}
{:4006}
' FIXME: duplicate of 4042
	cm$="Connected":gosub 9:&,69,5,21,cm$,$85
{:4008}
	lt$=d1$:lt%=mn%:poke 53248,.:mm=.:f1=.:goto {:3000}
{:4010}
	&,18,1:an$=d3$:&,15,1:&,69,5,17,an$,$8c:&,69,5,18,d2$,$8c:&,69,5,19,d6$,$8c
	&,69,5,20,l2$,$8c:if pt%then:&,69,16,20,"*PRIMETIME*",$8c
	&,52,41,3:&,69,31,17," Res O"+mid$("ff n  ",a%*3+1,3),$8c:kk=a%
	&,52,40,3:&,69,31,18," Res O"+mid$("ff n  ",a%*3+1,3),$8c:kk=kk+2*a%
	if l2=.anda%=.then:&,69,31,19," None    ",$8c
	if l2=1then:&,69,31,19," One     ",$8c
	if l2>1then:&,69,31,19," All     ",$8c
	&,69,31,20,"         ",$8c:&,69,31,20,str$(qb),$8c
	&,69,31,21,"         ",$8c:&,69,31,21,str$(uh)+"/"+mid$(str$(ur),2),$8c
	&,69,2,22,right$(str$(ca+1e5),5),$81:&,69,10,22,right$(str$(cn+1e5),3),$81
	return
' FIXME: orphaned code
' &,52,2 Acs-L: Editing User's access?
' &,52,4 Loc-L: Local mode?
' &,52,6 Tsr-L: Editing Time Left?
	&,52,2,3:a=a%:&,52,4,3:a=a+a%:&,52,6,3:a=a+a%:return
{:4040}
' FIXME: duplicate writes
	cm$="System Idle":gosub 9:&,69,5,21,cm$,$8a:return
' 4042
	cm$="Connected":gosub 9:&,69,5,21,cm$,$85:return
{:4044}
' &,52,7 Tsr-R: Prime Time enabled?
	gosub 84:&,52,7,3:if a% then pt%=1-pt%:if p1%<1 then p1%=30
	b%=val(mid$(fl$,18,2)):b%=b%-101*(b%=.):if pt%=. then return
	a$="PRIME":c=135:if pt% then a$="{space:5}":c=140
 	for y=1 to 5:&,69,26,y+16,mid$(a$,y,1),c:next:return

{:4100}
	a=.:gosub 375:b$="logoff":x=10:mm=4:gosub {:3000}:&,52,4:&,52,5:tr%=.
	f1=.:&,28,1:dr=3:b$="e.idle cmds":a$=b$:gosub 4:if e%then wc=.:close2:goto {:4106}
	wc=1:dimwc%(15,30):&,60,2,0,wc%(0,0),b$,dv%
{:4106}
	im=.:gosub 446:if ac%=16then mm=5:gosub {:3000}
	ak$=" {$c0:38}"+"{f6}":pf=.:poke 951,40
	gosub 33:x=17:gosub 1:&,2,2:d3$=a$:x=19:gosub 1:&,2,2:close2
	an$=a$:&,15:d2$=an$+"M":uf$="01000"+tz$:im=6:gosub 447
	lc=.:em=.:zz=.:gs=.:cf=.:cr=.:lf=1:nl=.:ul=.:le=.:pm=1:c9=.
	&,52,2,3:a=a%:&,52,4,3:a=a+a%:&,52,6,3:a=a+a%:if a=.then:&,52,2,4
	i%=.:ac%=16:dc%=.:ct%=.:tc%=.:u$="":sy$="":ba$="":na$="":ph$="":id=.
	poke 830,3:poke 53248,.:&,52,16,1:&,52,17,1:&,52,18,.:&,52,19,.
	gosub 82:gosub 12:forj=217to242:poke j,peek(j)or128:next:dr=3:gosub 61
	gosub 33:x=21:gosub 1:&,2,2:l2=val(a$):l2$=mid$(a$,3):close2
	gosub {:4010}:if peek(1010)then a$=d1$:t1=mn%
	a$="clock":gosub 28:goto {:4200}
{:4200}
	q=.:k=1:im=10:gosub 446:poke 53248,.:poke 53252,.:on f3 gosub {:4400},{:4420},{:4430}
{:4202}
	t1%=mn%:ti$="000000"
{:4204}
	if ti>36000 then gosub {:4044}
	mm=6:on -(ti>36000) gosub {:3000}:if mn%<>t1% then t1%=mn%
	&,52,41,3:z%=a%:&,52,40,3:z%=z%+2*a%:if z%<>kk then gosub {:4010}
	&,52,4,3:zz=a%:if zzthen im=4:gosub 446:goto {:4008}
	if peek(1010)>.andmid$(am$,2,6)<>mid$(d1$,2,6)then mm=7:goto {:3000}
	&,52,29,3:if peek(1010)>.anda%then mm=3:goto {:3000}
	poke 646,df%:&,16:a$="":if peek(198) then get a$
' 10 b=1:on b+1 goto 20:print"b<>0":end
' 20 ?"b=0"
' see "i.edit idlecmds" for info:
	on -(a$="") goto {:4230}:poke 198,.:if wc=. then {:4222}
	b=.:lm=1:for i=1 to wc%(0,0):if asc(a$)=wc%(0,i) then b=i:i=wc%(0,0)+1
	next:on b+1 goto {:4224}:&,60,1,16,wc%(8,b),cm$:gosub 9:&,60,1,11,wc%(4,b),a$
	lm=wc%(1,b):mm=8:goto 72
{:4222}
	x=val(a$):if xthen onxgoto {:4450},{:4465},{:4465},{:4465},{:4462},{:4464},{:4452},{:4454}
	if a$="R"then b$="reload":x=11:mm=4:gosub {:3000}:f1=3:pr$="":goto 302
{:4224}
	x=.:x=-(a$="+")-2*(a$="-")-3*(a$="{pound}")-4*(a$="T")-5*(a$="A")-6*(a$="{back arrow}")
	onx+1goto {:4230},,,,{:4259},{:4258},{:4260}:onxgosub {:4250},{:4252},{:4254},{:4256}:goto {:4232}
{:4230}
	k=k+(k>.):on-(a$=" "ork=1)goto {:4200}:on-(a$<>""and(a$<"{f1}"ora$>"{f8}"))gosub {:4270}
' Mnt-R: Disable Modem Answer
	&,52,43,3:if a% then {:4204}
{:4232}
	im=5:gosub 446:onagoto {:4006},{:4264},{:4266}:goto {:4204}
{:4250}
	mf=1:a$="m1":im=8:goto 446
{:4252}
	mf=.:a$="m0":im=8:goto 446
{:4254}
	im=7:goto 446
{:4256}
' modem carrier xor value:
	poke 53258,16-peek(53258):return
{:4258}
	mm=7:goto {:3000}
{:4259}
' modem carrier xor value:
	poke 53258,16-peek(53258):goto {:4204}
{:4260}
	poke 2042,.:poke 53248,1:&"{clear}{f6:2}Loading Image-Term..{f6}":gosub 304:b$="term":x=11
	mm=4:gosub {:3000}:a$="t":goto 70
{:4264}
	cm$="Ring Detected":gosub 9:&,69,5,21,cm$,$83:goto {:4202}
{:4266}
	gosub {:4040}:goto {:4204}
{:4270}
' TODO: use blocks free display subroutine?
	on -(a$="?")goto {:4300}:poke 53248,1:poke 971,16:&"{clear}{f6}{cyan}{$ac}{pound}o{$a2}{pound}o{$a2}{pound}h1{lt. blue}{$bb}{f6}"
	&"{cyan}{rvrs on}{$a1}{lt. blue}1) Set Time{pound}{back arrow}202) View BAR{pound}{back arrow}38{blue}{rvrs off}{$a1}{f6}{cyan}{rvrs on}{$a1}{lt. blue}3) View Blocks Free{pound}{back arrow}204) View"
	&" Clock/Calls{blue}{rvrs off}{$a1}{f6}{cyan}{rvrs on}{$a1}{lt. blue}5) Garbage Collect{pound}{back arrow}206) Reset Modem{pound}{back arrow}38{blue}{rvrs off}{$a1}{f6}"
	&"{cyan}{rvrs on}{$a1}{lt. blue}7) Set Password{pound}{back arrow}208) View Logs{pound}{back arrow}38{blue}{rvrs off}{$a1}{f6}{cyan}{rvrs on}{$a1}{lt. blue}+) Speaker On{pound}{back arrow}20-)Speaker "
	&"Off{pound}{back arrow}38{blue}{rvrs off}{$a1}{f6}{cyan}{rvrs on}{$a1}{lt. blue}{back arrow})Image-Term{pound}{back arrow}20A)Auto-Maint{pound}{back arrow}38{blue}{rvrs off}{$a1}{f6}{cyan}{rvrs on}{$a1}{lt. blue}R)Re-Load LO{pound}{back arrow}20"
' more "e.idle cmds" stuff:
	&"T)Invert Carrier  {blue}{rvrs off}{$a1}{f6}":if wc%(.,.)=. then {:4290}
	a=.:for i=1 to wc%(.,.):a$=mid$("{cyan}{rvrs on}{$a1}{lt. blue}",a*4+1):e$=mid$("{pound}{back arrow}38{blue}{rvrs off}{$a1}{f6}{pound}{back arrow}20",(1-a)*8+1,8)
	on -(wc%(0,i)=0) goto {:4288}:b$=chr$(wc%(.,i)):&,60,1,16,wc%(8,i),c$
	&a$+b$+")"+c$+e$:a=1-a
{:4288}
	next:if a=1 then:&"{pound}{back arrow}38{blue}{rvrs off}{$a1}{f6}"
{:4290}
	&"{lt. blue}{$bc}{blue}{rvrs on}{pound}o{$a2}{pound}o{$a2}{pound}h1{rvrs off}{$be}{f6}":poke 53248,.:poke 2042,.:k=50:return
{:4300}
	a$="":goto {:4270}:rem "GM will be here"
{:4400}
	f3=1:poke 53248,1:poke 2042,1:x%=st(20):y%=st(24):z%=st(23):w%=st(29)
	&"{clear}{f6}{pound}{back arrow}22{lt. red}{rvrs on} Feedback   {pound}#3{pound}# {pound}%z {f6}{pound}{back arrow}22{rvrs on} SysOp Mail {pound}#3{pound}# {pound}%y {gray1} {f6}"
	&"{pound}{back arrow}22{lt. red}{rvrs on} Calls/Log  {pound}#3{pound}# {pound}%x {gray1} {f6}{pound}{back arrow}22{lt. red}{rvrs on} New Users  {pound}#3{pound}# {pound}%w {gray1} {f6}"
	&"{pound}{back arrow}23{rvrs on}                {f6}{up}":&,27,3:diml%(30,9):dr=3:gosub 3
{number:4408}
{step:1}
' d$=mm/dd @ t$=time m$=xxx min c$=caller
' &,60,2: load "e.last" struct
' gosub 5: check DOS error status, e%=error
	&,60,2,.,l%(.,.),dr$+"e.last",dv%:gosub 5:if e% then {:4416}
' 4409: check Alt status just once, get m$=minutes
' &,52,$36,3: check status of Alt-L on lightbar, put result in a%
 	&,52,$36,3:m$="{pound}# {pound}#4{pound}%m Min ":for i=. to 9:m%=l%(7,i)
' 4410: getString: c$=caller, getDate: an$=date wmmddyyhhmm
	&,60,1,20,l%(8,i),c$:&,60,5,.,l%(1,i),an$:if len(c$)<4 then {:4415}
' 4411: format d$="mm/dd" from short date; &,15: get long date, t$="hh:mm xM"
	d$=mid$(an$,4,2)+"/"+mid$(an$,6,2):&,15:t$=right$(an$,7)+"M"
' 4412:
	if a%=. then:&"{f6}{rvrs on}{gray2}{pound}$d @ {pound}$t"+m$
{:4413}
	if a% then:&"{f6}{lt. blue}{pound}$d {yellow}@ {lt. blue}{pound}$t{white}"+m$+"{yellow}"
{:4414}
	&"{pound}$c{pound}{back arrow}39{rvrs off}"
{:4415}
	next
{:4416}
	&,28,3:close 2:poke 53248,.:goto {:4040}

{:4420}
	f3=2:poke 53248,1:poke 2042,.:&"{clear}":im=2:a$="bar":gosub 74:poke 53248,.:goto {:4040}
{:4430}
	f3=3:poke 53248,1:poke 2042,.:&"{clear}{f6} {rvrs on}{cyan} Blocks Free On System Disks {f6:2}"
	dr=1
	b$="System":d$="Mail":gosub {:4438}:b$="Etc.":d$="Dir.":gosub {:4438}
	b$="Program":d$="User":gosub {:4438}:&"{f6:2}":poke 53248,.:poke 198,.:goto {:4040}
{:4438}
	c$=str$(bf(dr)):f$=str$(bf(dr+1))
	a$="{lt. red} {pound}$b {gray3}Disk{white}:{pound}{back arrow}15{pound}$c{f6}{lt. red} {pound}$d {gray3}Disk{white}:{pound}{back arrow}15{pound}$f{f6}":&:dr=dr+2:return
{:4450}
	lm=1:goto {:4460}
{:4452}
	lm=2:goto {:4460}
{:4454}
	lm=3:goto {:4460}
{:4460}
	z$="lo."+"misc":gosub 80:goto {:4200}
{:4462}
	&,40:goto {:4204}
{:4464}
	mm=6:gosub {:3000}:goto {:4204}
{:4465}
	onxgosub ,{:4420},{:4430},{:4400}:goto {:4204}
{:4480}
	&,52,$34,3:if a%then poke 53371,0:&,52,$35,1
	return
	rem i/lo.idle (c)nissa, 2022-10-07 lh+rs
